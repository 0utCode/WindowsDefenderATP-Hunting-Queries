// Query #1: get machines that have used a given local IP address at a given time - as configured on their network adapters
let pivotTimeParam = datetime(2018-07-15 19:51:00);
let ipAddressParam = "192.168.1.5";
MachineNetworkInfo
| where EventTime between ((pivotTimeParam-15m) ..30m) and IPAddresses contains strcat("\"", ipAddressParam, "\"") and NetworkAdapterStatus == "Up"
//// Optional - add filters to make sure machine is part of the relevant network (and not using that IP address as part of another private network).
//// For example:
// and ConnectedNetworks contains "corp.contoso.com"
// and IPv4Dhcp == "10.164.3.12"
// and DefaultGateways contains "\"10.164.3.1\""
| project ComputerName, EventTime, IPAddresses, TimeDifference=abs(EventTime-pivotTimeParam)
// In case multiple machines have reported from that IP address arround that time, start with the ones reporting closest to pivotTimeParam
| sort by TimeDifference asc

// Query #2:
// same as query #1 (get machines that have used a given local IP address at a given time), but also query for the logged on user
let pivotTimeParam = datetime(2018-07-15 19:51:00);
let ipAddressParam = "192.168.1.5";
let matchingMachines = 
    MachineNetworkInfo
    | where EventTime between ((pivotTimeParam-15m) ..30m) and IPAddresses contains strcat("\"", ipAddressParam, "\"") and NetworkAdapterStatus == "Up"
    //// Optional - add filters to make sure machine is part of the relevant network (and not using that IP address as part of another private network).
    //// For example:
    // and ConnectedNetworks contains "corp.contoso.com"
    // and IPv4Dhcp == "10.164.3.12"
    // and DefaultGateways contains "\"10.164.3.1\""
    | project ComputerName, EventTime, IPAddresses, TimeDifference=abs(EventTime-pivotTimeParam);
MachineInfo
| where EventTime between ((pivotTimeParam-15m) ..30m)
| project ComputerName, EventTime, LoggedOnUsers 
| join kind=inner (matchingMachines) on ComputerName, EventTime
| project EventTime, ComputerName, LoggedOnUsers, TimeDifference, IPAddresses
// In case multiple machines have reported from that IP address arround that time, start with the ones reporting closest to pivotTimeParam
| sort by TimeDifference asc

// Query #3: get machines that have used a given *public* IP address at a given time - as seen in their communications with the WDATP cloud
let pivotTimeParam = datetime(2018-07-15 19:51:00);
let ipAddressParam = "192.168.1.5";
MachineInfo
| where EventTime between ((pivotTimeParam-15m) .. 30m) and PublicIP == ipAddressParam
| project ComputerName, LoggedOnUsers, EventTime, TimeDifference=abs(EventTime-pivotTimeParam)
// In case multiple machines have reported from that IP address arround that time, start with the ones reporting closest to pivotTimeParam
| sort by TimeDifference asc

// Query #4: Get machines that have used a given IP address, looking up on both local and external addresses.
//           This includes IP addresses seen locally in their network adapters configuration or ones used to access the WDATP cloud.
let pivotTimeParam = datetime(2018-07-15 19:51:00);
let ipAddressParam = "192.168.1.5";
MachineNetworkInfo
| where EventTime between ((pivotTimeParam-15m) ..30m) and IPAddresses contains strcat("\"", ipAddressParam, "\"") and NetworkAdapterStatus == "Up"
| project ComputerName, EventTime, Source="NetworkAdapterInfo" 
| union (MachineInfo | where EventTime between ((pivotTimeParam-15m) .. 30m) and PublicIP == ipAddressParam | project ComputerName, EventTime, Source="Public IP address")
| extend TimeDifference=abs(EventTime-pivotTimeParam)
| sort by TimeDifference asc
